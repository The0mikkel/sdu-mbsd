grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Maths:
	program=Program &
	externals+=(External)* &
	expressions+=(MathExp)+
;

Program:
	'program' name=ID
;

External:
	'external' name=ID '(' (parameters+=ExternalInput ','?)* ')'
;

ExternalInput:
	'int'
;

MathExp:
	'var' variable=Variable exp=Exp
;

Variable:
	name=ID '='
;

Exp:
	expression=AddOrSub
;

SubMathExp: 
	'let' variable=Variable value=Exp 'in' exp+=Exp 'end'
;

ExternalCall:
 	method=[External] '(' attributes=ExternalAttribute? ')'
;

ExternalAttribute: Exp ( {ExternalAttribute.left=current} ',' right=Exp)* ;


AddOrSub returns Expression: Term ( ({Plus.left=current} '+' | {Minus.left=current} '-') right=Term )* ;
Term returns Expression: SubMath (({Multi.left=current} '*' | {Div.left=current} '/') right=SubMath)* ;
SubMath returns Expression: Factor ({SubMath.left=current} sub=SubMathExp)* ;
Factor returns Expression: Number | '(' Exp ')' | VariableUse | SubMathExp | ExternalCall;

Number:
	value=INT
;

VariableUse:
	ref = [Variable] // To handle two different types, "Def" could have been used instead
;

Def:
	MathExp | SubMathExp
;
